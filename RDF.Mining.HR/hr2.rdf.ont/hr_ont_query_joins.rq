PREFIX JobSeeOnt: 	<http://www.example.com/JobSeeker_Ontology#> 
PREFIX rdf: 		<http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX owl: 		<http://www.w3.org/2002/07/owl#> 
PREFIX xsd: 		<http://www.w3.org/2001/XMLSchema#> 
PREFIX CompOnt: 	<http://example.com/Competence_Ontology#> 
PREFIX SkillOnt: 	<http://example.com/Skill_Ontology#> 
PREFIX rdfs: 		<http://www.w3.org/2000/01/rdf-schema#> 
PREFIX foaf: 		<http://xmlns.com/foaf/0.1/> 

SELECT 
	?Job_Seeker 
	?Education_Title 
		?Education_Title_Spec
	?Java_Programming_Skills 
			?SQL_Programming_Skills 
			?NOSQL_Programming_Skills
			?UML_Skills
			?SOA_Developer_Skills
			?Java_Web_Developer_Skills
			?Web_Developer_Skills
			?DB_Developer_Skills
			?Java_Persistence_Skills	
	#?Years_Experience
	(IF(BOUND(?Years_Experience),?Years_Experience, "?") as ?Years_Experience_last)  
			(IF(BOUND(?Position_held_count),?Position_held_count, "?") as ?Position_count) 
			(IF(BOUND(?Years_to_position),?Years_to_position, "?") as ?Years_experience_position) 
	# ?Position_held_last
			(IF(BOUND(?Position_held_last),?Position_held_last, "?") as ?Position_held)
WHERE
{
	### SKILLS
	OPTIONAL {
		SELECT 
			?Job_Seeker #?last_name 
			?skillDescription
			?Java_Programming_Skills 
			?SQL_Programming_Skills 
			?NOSQL_Programming_Skills
			?UML_Skills
			?SOA_Developer_Skills
			?Java_Web_Developer_Skills
			?Web_Developer_Skills
			?DB_Developer_Skills
			?Java_Persistence_Skills
		WHERE
		 { 	?Job_Seeker a JobSeeOnt:Job_Seeker .
		 	#?Job_Seeker JobSeeOnt:Last_Name 	?last_name .
		 	?Job_Seeker JobSeeOnt:has_candidacy ?candidacy .
		 	?competence CompOnt:is_competence_of_Candidacy ?candidacy .
		 	?skill rdfs:subClassOf ?competence .
		 	?skill SkillOnt:Description ?skillDescription
		 	BIND(	
				CONTAINS(UCASE(?skillDescription), "JAVA") ||
				CONTAINS(UCASE(?skillDescription), "JSE") ||
				CONTAINS(UCASE(?skillDescription), "JEE") ||
				CONTAINS(UCASE(?skillDescription), "J2SE") ||
		 		CONTAINS(UCASE(?skillDescription), "J2EE")
		 	AS ?Java_Programming_Skills)
		 	BIND(	
		 		CONTAINS(UCASE(?skillDescription), "SQL") ||
		 		CONTAINS(UCASE(?skillDescription), "Oracle")	 
		 	AS ?SQL_Programming_Skills)
		 	BIND(	
		 		CONTAINS(UCASE(?skillDescription), "NOSQL") ||
		 		CONTAINS(UCASE(?skillDescription), "MONGO DB")	 
		 	AS ?NOSQL_Programming_Skills)
		 	BIND(	
		 		CONTAINS(UCASE(?skillDescription), "UML")	 
		 	AS ?UML_Skills)
		 	BIND(
		 		CONTAINS(UCASE(?skillDescription), "WEB SERVICE") ||	
		 		CONTAINS(UCASE(?skillDescription), "SOA") ||
		 		CONTAINS(UCASE(?skillDescription), "REST") ||
		 		CONTAINS(UCASE(?skillDescription), "SOAP") ||
		 		CONTAINS(UCASE(?skillDescription), "JAX-RS") ||
		 		CONTAINS(UCASE(?skillDescription), "JAX-WS")	 
		 	AS ?SOA_Developer_Skills)
		 	BIND(	
		 		CONTAINS(UCASE(?skillDescription), "SERVLET") ||
		 		CONTAINS(UCASE(?skillDescription), "JSP") ||
		 		CONTAINS(UCASE(?skillDescription), "JSF") ||
		 		CONTAINS(UCASE(?skillDescription), "STRUTS")	 
		 	AS ?Java_Web_Developer_Skills)
			BIND(	
		 		CONTAINS(UCASE(?skillDescription), "HTML") ||
		 		CONTAINS(UCASE(?skillDescription), "JAVASCRIPT") ||
		 		CONTAINS(UCASE(?skillDescription), "JQUERY") ||
		 		CONTAINS(UCASE(?skillDescription), "ANGULARJS")	 
		 	AS ?Web_Developer_Skills) 	
			BIND(	
		 		CONTAINS(UCASE(?skillDescription), "SQL") ||
		 		CONTAINS(UCASE(?skillDescription), "ORACLE") ||
		 		CONTAINS(UCASE(?skillDescription), "MYSQL") ||
		 		CONTAINS(UCASE(?skillDescription), "POSTGRES")	 
		 	AS ?DB_Developer_Skills)  	
			BIND(	
		 		CONTAINS(UCASE(?skillDescription), "JDBC") &&
		 		  ( CONTAINS(UCASE(?skillDescription), "JPA") ||
		 			CONTAINS(UCASE(?skillDescription), "JAVA PERSISTENCE API") ||
		 			CONTAINS(UCASE(?skillDescription), "HIBERNATE") ) 
		 	AS ?Java_Persistence_Skills)  	
		 }
	}
	### POSITIONS
	OPTIONAL {
		SELECT 
			?Job_Seeker 
			(MAX(?End_date_max) AS ?End_date_last)
			(group_concat(?Position_held_parsed;separator="; ") as ?Position_held_parsed_concat) 
			(replace(group_concat(?Position_held_parsed;separator="; "),".*;","") AS ?Position_held_last)
		WHERE
		{
			?Job_Seeker a JobSeeOnt:Job_Seeker . 
			?Job_Seeker JobSeeOnt:has_candidacy ?candidacy . 
			?candidacy JobSeeOnt:has_work_experience ?experience . 
			?experience JobSeeOnt:End_Date ?End_date . 
			?experience JobSeeOnt:Position_held ?Position_held . 
		 	BIND((IF (REPLACE(?End_date, "  ","")  = "Prezent", 2017, 
		 		xsd:integer(SUBSTR(?End_date, STRLEN(?End_date)-3, 4)))) AS ?End_date_max)
			BIND(
				IF(CONTAINS(UCASE(?Position_held), "JAVA") 
						,"JAVA-DEVELOPER",
					IF((CONTAINS(UCASE(?Position_held), "JEE") 
						|| CONTAINS(UCASE(?Position_held), "J2EE")) 
							,"JEE-DEVELOPER", 
						IF(CONTAINS(UCASE(?Position_held), "SOFTWARE ENGINEER")
							,"SOFTWARE-ENGINEER",
							IF(CONTAINS(UCASE(?Position_held), "DEVELOPER"),
								"OTHER-DEVELOPER",
								IF(CONTAINS(UCASE(?Position_held), "PROGRAMER"),
									"OTHER-PROGRAMER",
									IF(CONTAINS(UCASE(?Position_held), "ANALYST"),
										"ANALYST",
										IF(CONTAINS(UCASE(?Position_held),"DEVELOPMENT"),
											"OTHER-DEVELOPMENT",
											IF(CONTAINS(UCASE(?Position_held),"ENGINEER"),
												"OTHER-ENGINEER","OTHER" ))))))))
			AS ?Position_held_parsed)  				 	
		 	FILTER(?End_date_max > 0)
		 }
		 GROUP BY ?Job_Seeker
	}
	### EXPERIENCE_YEARS
	OPTIONAL {
		SELECT 
			?Job_Seeker  
			#?last_name 
			?Years_Experience 
			?Position_held_count 
			?Years_to_position
		WHERE 
		{
			{
				SELECT 
					?Job_Seeker  
					#?last_name  
					(MIN(?Begin_date_parsed) AS ?Begin_date_min) 
					(MAX(?End_date_parsed) AS ?End_date_max) 
					(COUNT(?Position_held) AS ?Position_held_count)
					(MAX(?End_date_parsed) - MIN(?Begin_date_parsed) AS ?Years_Experience)
					#?Begin_date
					#?Begin_date_parsed
					#?End_date
					#?End_date_parsed
				WHERE
				 { 	?Job_Seeker a JobSeeOnt:Job_Seeker .
				 	#?Job_Seeker JobSeeOnt:Last_Name 	?last_name .
				 	?Job_Seeker JobSeeOnt:has_candidacy ?candidacy .
				 	?candidacy JobSeeOnt:has_work_experience ?experience .
				 	?experience JobSeeOnt:Position_held ?Position_held .
				 	?experience JobSeeOnt:Begin_date ?Begin_date .
				 	?experience JobSeeOnt:End_Date ?End_date .
				 	BIND((IF (REPLACE(?Begin_date, "  ","")  = "Prezent", 2017, 
				 		xsd:integer(SUBSTR(?Begin_date, STRLEN(?Begin_date)-1, 2)) + 2000)) AS ?Begin_date_parsed)
				 	BIND((IF (REPLACE(?End_date, "  ","")  = "Prezent", 2017, 
				 		xsd:integer(SUBSTR(?End_date, STRLEN(?End_date)-3, 4)))) AS ?End_date_parsed)
				 	FILTER(?End_date_parsed > 0)
				 }
				 GROUP BY ?Job_Seeker  #?last_name
			 }
			 BIND((?Years_Experience/?Position_held_count) AS ?Years_to_position)
		 }	
	}
	### EDUCATION
	OPTIONAL {
		SELECT 
			?Job_Seeker 
			#?last_name 
			?Title 
			(IF(?Title = "-","?",?Title)  as ?Education_Title)
			?Specialization
			(
				IF(CONTAINS(UCASE(?Specialization), "COMPUTER SCIENCE") || CONTAINS(UCASE(?Specialization), "CSE")
					|| CONTAINS(UCASE(?Specialization), "COMPUTERS SCIENCE"), "Computer-Science",
			 		IF(CONTAINS(UCASE(?Specialization), "INFORMATION TECHNOLOGY")
			 			|| CONTAINS(UCASE(?Specialization), "INFORMATION TECHNOLOGIES"), "Information-Technology", 
						IF(CONTAINS(UCASE(?Specialization), "ELECTRONICS") || CONTAINS(UCASE(?Specialization), "ELECTRICAL"),"Electronics",
							IF(CONTAINS(UCASE(?Specialization), "COMPUTER ENGINEERING"),"Computer-Engineering",
								IF(CONTAINS(UCASE(?Specialization), "SOFTWARE ENGINEERING"),"Software-Engineering",
									IF(CONTAINS(UCASE(?Specialization), "COMPUTER APPLICATIONS")
										|| CONTAINS(UCASE(?Specialization), "COMPUTER APPLICATION"),"Computer-Applications","OTHER"
									)
								)
							)
						)
					)
				)
		 	AS ?Education_Title_Spec)			
		WHERE
		 { 	?Job_Seeker a JobSeeOnt:Job_Seeker .
		 	#?Job_Seeker JobSeeOnt:Last_Name 	?last_name .
		 	?Job_Seeker JobSeeOnt:has_candidacy ?candidacy .
		 	?candidacy JobSeeOnt:has_education ?education .
		 	?education JobSeeOnt:Title ?Title .
		 	?education JobSeeOnt:has_specialization ?Specialization .
		 }	
	}
	FILTER(?Education_Title != "")
 }
 ORDER BY ?Job_Seeker
 #LIMIT 100